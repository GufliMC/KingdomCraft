plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

repositories {
    mavenCentral()

    // Bukkit
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }

    // Vault
    maven { url 'https://nexus.hc.to/content/repositories/pub_releases' }

    // DiscordSRV
    maven { url 'https://nexus.scarsz.me/content/repositories/releases/' }
}

dependencies {
    implementation project(':api')
    implementation project(':common')

    compileOnly 'org.bukkit:bukkit:1.8.8-R0.1-SNAPSHOT'

    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly ('com.discordsrv:discordsrv:1.22.0') {
        transitive = false
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        expand 'pluginVersion': project.ext.fullVersion
        include 'plugin.yml'
    }
}

shadowJar {
    archiveBaseName.set("KingdomCraftStarter")
    archiveVersion.set("${project.ext.fullVersion}")

    dependencies {
        exclude(dependency(':junit'))
        exclude(dependency(':snakeyaml'))
        exclude(dependency(':gson'))
    }

//    doLast {
//        copy {
//            from "build/libs/" + archiveFileName.get()
//            into "D:/Documents/testservers/1.18/plugins"
//        }
//    }
}

artifacts {
    archives shadowJar
}